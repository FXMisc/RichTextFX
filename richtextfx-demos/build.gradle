plugins {
	id 'java'
}

dependencies {
    implementation project(":richtextfx")
}

jar {
    manifest {
        attributes(
            'Specification-Title': 'RichTextFX Demos',
            'Specification-Version': project.specificationVersion,
            'Implementation-Title': 'RichTextFX Demos',
            'Implementation-Version': project.version)
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    // avoid implicit dependency warning since we use output from the lib projects
    // See  https://docs.gradle.org/7.0.2/userguide/validation_problems.html#implicit_dependency
    dependsOn ":richtextfx:jar"

    archiveAppendix = 'fat'

    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

assemble.dependsOn fatJar

task demos(description: "Lists the names and the descriptions of the demos that can be run via a Gradle task") {
    doLast {
        // sort the demos first, so they can be added in the build file in any order
        def list = []
        tasks.withType(JavaExec) {
            if (it.name.endsWith("Demo")) {
                list.add(it)
            }
        }
        list.sort { first, next -> first.name <=> next.name }
                .each {
            println "  ${it.name} - ${it.description}"
        }
    }
}

// To add a task for a demo, use the following template:
/*
task ClassNameDemo(type: JavaExec, dependsOn: classes,
        description: "A description of what the demo does") {
    mainClass = 'org.fxmisc.richtext.demo.ClassNameDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}
 */

task JavaKeywordsDemo(type: JavaExec, dependsOn: classes,
        description: "A CodeArea with Java syntax highlighting that is computed on the JavaFX Application Thread") {
    mainClass = 'org.fxmisc.richtext.demo.JavaKeywordsDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task JavaKeywordsAsyncDemo(type: JavaExec, dependsOn: classes,
        description: "A CodeArea with Java syntax highlighting that is computed on a background thread") {
    mainClass = 'org.fxmisc.richtext.demo.JavaKeywordsAsyncDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task XMLEditorDemo(type: JavaExec, dependsOn: classes,
        description: "An area with XML syntax highlighting") {
    mainClass = 'org.fxmisc.richtext.demo.XMLEditorDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task ManualHighlightingDemo(type: JavaExec, dependsOn: classes,
        description: "Manually highlight various parts of the text in an area via buttons") {
    mainClass = 'org.fxmisc.richtext.demo.ManualHighlightingDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task RichTextDemo(type: JavaExec, dependsOn: classes,
        description: "An area showing a large number of RichTextFX's features: " +
                "inlined images, rich text (e.g. text alignment and background colors, etc.), and save/load capabilities") {
    mainClass = 'org.fxmisc.richtext.demo.richtext.RichTextDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task PopupDemo(type: JavaExec, dependsOn: classes,
        description: "A popup that follows the caret and selection when they move") {
    mainClass = 'org.fxmisc.richtext.demo.PopupDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task TooltipDemo(type: JavaExec, dependsOn: classes,
        description: "Tells you the letter over which the mouse is hovering") {
    mainClass = 'org.fxmisc.richtext.demo.TooltipDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task HyperlinkAreaDemo(type: JavaExec, dependsOn: classes,
        description: "An area with hyperlinks that open to their corresponding link") {
    mainClass = 'org.fxmisc.richtext.demo.hyperlink.HyperlinkDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task LineIndicatorDemo(type: JavaExec, dependsOn: classes,
        description: "Line numbers appear to left of each paragraph and a triangle appears on the same paragraph as the caret") {
    mainClass = 'org.fxmisc.richtext.demo.lineindicator.LineIndicatorDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task CloneDemo(type: JavaExec, dependsOn: classes,
        description: "Two areas that can modify and show the same underlying document") {
    mainClass = 'org.fxmisc.richtext.demo.CloneDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task FontSizeSwitcherDemo(type: JavaExec, dependsOn: classes,
        description: "Change the font size of the entire area.") {
    mainClass = 'org.fxmisc.richtext.demo.FontSizeSwitcherDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task MultiCaretAndSelectionNameDemo(type: JavaExec, dependsOn: classes,
        description: "Add and display multiple carets and selections with different style classes in the same area") {
    mainClass = 'org.fxmisc.richtext.demo.MultiCaretAndSelectionDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task OverrideBehaviorDemo(type: JavaExec, dependsOn: classes,
        description: "Overrides the area's default behavior and demonstrates some things of which to be aware") {
    mainClass = 'org.fxmisc.richtext.demo.OverrideBehaviorDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task ShowLineDemo(type: JavaExec, dependsOn: classes,
        description: "Force a specific part of the underlying document to be rendered to the screen.") {
    mainClass = 'org.fxmisc.richtext.demo.ShowLineDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}

task SpellCheckingDemo(type: JavaExec, dependsOn: classes,
        description: "Shows how to add a red squiggle underneath misspelled words") {
    mainClass = 'org.fxmisc.richtext.demo.SpellCheckingDemo'
    classpath = files(sourceSets.main.output, configurations.runtimeClasspath)
}
