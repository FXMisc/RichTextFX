buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.fxmisc.richtext'

testSets {
    integrationTest { dirName = 'integrationTest' }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
integrationTest.testClassesDirs = sourceSets.integrationTest.output.classesDirs

sourceSets {
   java9 {
      java {
         srcDirs = ['src/main/java9']
      }
   }
}

dependencies {
    compile group: 'org.reactfx', name: 'reactfx', version: '2.0-M5'
    compile group: 'org.fxmisc.undo', name: 'undofx', version: '2.1.0'
    compile group: 'org.fxmisc.flowless', name: 'flowless', version: '0.6'
    compile group: 'org.fxmisc.wellbehaved', name: 'wellbehavedfx', version: '0.3.3'

    java9Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }

    testCompile group: 'junit', name: 'junit', version: '4.12'

    integrationTestCompile group: 'junit', name: 'junit', version: '4.12'
    integrationTestCompile group: 'com.nitorcreations', name: 'junit-runners', version: '1.2'
    integrationTestCompile "org.testfx:testfx-core:4.0.8-alpha"
    if ( JavaVersion.current() >= JavaVersion.VERSION_1_9 ) {
        integrationTestCompile "org.testfx:testfx-internal-java9:4.0.8-alpha"
    }
    integrationTestCompile ("org.testfx:testfx-junit:4.0.8-alpha") {
        exclude(group: "junit", module: "junit")
    }
    integrationTestCompile "org.testfx:openjfx-monocle:8u76-b04"
}

compileJava {
   if ( JavaVersion.current() == JavaVersion.VERSION_1_9 ) {
      sourceCompatibility = 8
      targetCompatibility = 8
   }
   options.deprecation = true
}

compileJava9Java {
   onlyIf { JavaVersion.current() == JavaVersion.VERSION_1_9 }
   sourceCompatibility = 9
   targetCompatibility = 9
   options.deprecation = true
}

jar {
    into 'META-INF/versions/9', { from sourceSets.java9.output }
    manifest {
        attributes(
            'Specification-Title': 'RichTextFX',
            'Specification-Version': project.specificationVersion,
            'Implementation-Title': 'RichTextFX',
            'Implementation-Version': project.version,
            'Multi-Release': 'true')
    }
}

javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')

    // support for JavaFX properties
    options.addBooleanOption('javafx', true);

    options.links = [
        // resolve links to Java and JavaFX Javadocs
        'http://docs.oracle.com/javase/8/docs/api/',
        'http://docs.oracle.com/javase/8/javafx/api/',

        // resolve links to ReactFX
        'http://www.reactfx.org/javadoc/2.0-M5/',

        // resolve links to FXMisc projects
        'https://fxmisc.github.io/undo/javadoc/1.3.1/',
        'https://fxmisc.github.io/flowless/javadoc/0.6/',
        'https://fxmisc.github.io/wellbehaved/javadoc/0.3/'
    ]
}

// use test logging.
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

test {
    dependsOn jar
    // run tests against jar in case it's a  multi-release jar
    classpath = files(jar.archivePath, classpath) - sourceSets.main.output
    testLogging {
        // Fancy formatting from http://stackoverflow.com/a/36130467/3634630
        // set options for log level LIFECYCLE
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
        showExceptions true
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED, TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED, TestLogEvent.FAILED,
                    TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
        }
        info.events = debug.events
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

integrationTest {
    dependsOn jar
    // run tests against jar in case it's a  multi-release jar
    classpath = files(jar.archivePath, classpath) - sourceSets.main.output
    testLogging {
        // Fancy formatting from http://stackoverflow.com/a/36130467/3634630
        // set options for log level LIFECYCLE
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
        showExceptions true
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED, TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED, TestLogEvent.FAILED,
                    TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
        }
        info.events = debug.events
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    into 'java9', { from sourceSets.java9.allSource }
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

def doUploadArchives = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')

if(doUploadArchives) {
    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'RichTextFX'
                packaging 'jar'
                description 'Rich-text area for JavaFX'
                url 'http://www.fxmisc.org/richtext/'
           
                scm {
                    url 'scm:git@github.com:FXMisc/RichTextFX.git'
                    connection 'scm:git@github.com:FXMisc/RichTextFX.git'
                    developerConnection 'scm:git@github.com:FXMisc/RichTextFX.git'
                }
           
                licenses {
                    license {
                        name 'The BSD 2-Clause License'
                        url 'http://opensource.org/licenses/BSD-2-Clause'
                        distribution 'repo'
                    }
                    license {
                        name 'GPLv2 with the Classpath Exception'
                        url 'http://www.gnu.org/software/classpath/license.html'
                        distribution 'repo'
                    }
                }
           
                developers {
                    developer {
                        name 'Tomas Mikula'
                    }
                }
            }
        }
    }
}

uploadArchives.onlyIf { doUploadArchives }

task fatJar(type: Jar, dependsOn: classes) {
    appendix = 'fat'
    manifest.attributes( 'Multi-Release': 'true' )
    into 'META-INF/versions/9', { from sourceSets.java9.output }
    from sourceSets.main.output
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

assemble.dependsOn fatJar
