buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id 'biz.aQute.bnd.builder' version "5.3.0"
    id 'maven-publish'
    id 'signing'
}

group = 'org.fxmisc.richtext'

testSets {
    integrationTest { dirName = 'integrationTest' }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
integrationTest.testClassesDirs = sourceSets.integrationTest.output.classesDirs

sourceSets {
   java9 {
      java {
         srcDirs = ['src/main/java9']
      }
   }
}

dependencies {
    implementation group: 'org.reactfx', name: 'reactfx', version: '2.0-M5'
    implementation group: 'org.fxmisc.undo', name: 'undofx', version: '2.1.1'
    api group: 'org.fxmisc.flowless', name: 'flowless', version: '0.6.3'
    api group: 'org.fxmisc.wellbehaved', name: 'wellbehavedfx', version: '0.3.3'

    java9Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    integrationTestImplementation group: 'junit', name: 'junit', version: '4.12'
    integrationTestImplementation group: 'com.nitorcreations', name: 'junit-runners', version: '1.2'
    integrationTestImplementation "org.testfx:testfx-core:4.0.16-alpha"
    integrationTestImplementation ("org.testfx:testfx-junit:4.0.15-alpha") {
        exclude(group: "junit", module: "junit")
    }
    integrationTestImplementation "org.testfx:openjfx-monocle:8u76-b04"
}

compileJava {
   if ( JavaVersion.current() == JavaVersion.VERSION_1_9 ) {
      sourceCompatibility = 8
      targetCompatibility = 8
   }
   options.deprecation = true
}

compileJava9Java {
   onlyIf { JavaVersion.current() == JavaVersion.VERSION_1_9 }
   sourceCompatibility = 9
   targetCompatibility = 9
   options.deprecation = true
}

jar {
    // Workaround for the missing multi release jar support in the BND tool
    // See https://github.com/bndtools/bnd/issues/2227
    bnd('-fixupmessages': '^Classes found in the wrong directory: .*',
        '-exportcontents': 'org.fxmisc.richtext.*')

    into 'META-INF/versions/9', { from sourceSets.java9.output }
    manifest {
        attributes(
            'Specification-Title': 'RichTextFX',
            'Specification-Version': project.specificationVersion,
            'Implementation-Title': 'RichTextFX',
            'Implementation-Version': project.version,
            'Automatic-Module-Name': 'org.fxmisc.richtext',
            'Multi-Release': 'true')
    }
}

javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')

    // support for JavaFX properties
    options.addBooleanOption('javafx', true);

    options.links = [
        // resolve links to Java and JavaFX Javadocs
        'http://docs.oracle.com/javase/8/docs/api/',
        'http://docs.oracle.com/javase/8/javafx/api/',

        // resolve links to ReactFX
        'https://www.javadoc.io/doc/org.reactfx/reactfx/2.0-M5/',

        // resolve links to FXMisc projects
        'https://fxmisc.github.io/undo/javadoc/1.3.1/',
        'https://fxmisc.github.io/flowless/javadoc/0.6/',
        'https://fxmisc.github.io/wellbehaved/javadoc/0.3/'
    ]
}

// use test logging.
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

test {
    dependsOn jar
    // run tests against jar in case it's a  multi-release jar
    classpath = files(jar.archivePath, classpath) - sourceSets.main.output
    testLogging {
        // Fancy formatting from http://stackoverflow.com/a/36130467/3634630
        // set options for log level LIFECYCLE
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
        showExceptions true
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED, TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED, TestLogEvent.FAILED,
                    TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
        }
        info.events = debug.events
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

integrationTest {
    dependsOn jar
    // run tests against jar in case it's a  multi-release jar
    classpath = files(jar.archivePath, classpath) - sourceSets.main.output
    testLogging {
        // Fancy formatting from http://stackoverflow.com/a/36130467/3634630
        // set options for log level LIFECYCLE
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
        showExceptions true
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED, TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED, TestLogEvent.FAILED,
                    TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
        }
        info.events = debug.events
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    into 'java9', { from sourceSets.java9.allSource }
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def doUploadArchives = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
publish.onlyIf { doUploadArchives }

if(doUploadArchives) {
    publishing {
	    repositories {
	        maven {
	           def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
	           def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
	           url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
	            credentials {
	                username = "${sonatypeUsername}"
	                password = "${sonatypePassword}"
	            }
	        }
	    }
        publications {
            richTextFX(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
	                name = 'RichTextFX'
	                packaging = 'jar'
	                description = 'Rich-text area for JavaFX'
	                url = 'https://github.com/FXMisc/RichTextFX/#richtextfx'
	           
	                scm {
	                    url = 'scm:git@github.com:FXMisc/RichTextFX.git'
	                    connection = 'scm:git@github.com:FXMisc/RichTextFX.git'
	                    developerConnection = 'scm:git@github.com:FXMisc/RichTextFX.git'
	                }
	           
	                licenses {
	                    license {
	                        name = 'The BSD 2-Clause License'
	                        url = 'http://opensource.org/licenses/BSD-2-Clause'
	                        distribution = 'repo'
	                    }
	                    license {
	                        name = 'GPLv2 with the Classpath Exception'
	                        url = 'http://www.gnu.org/software/classpath/license.html'
	                        distribution = 'repo'
	                    }
	                }
	           
	                developers {
	                    developer {
	                        name = 'Tomas Mikula'
	                    }
	                }
	            }
            }
		}
    }
	
    signing {
        sign publishing.publications.richTextFX
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    archiveAppendix = 'fat'

    manifest.attributes( 'Automatic-Module-Name': 'org.fxmisc.richtext',
                         'Multi-Release': 'true' )
    into 'META-INF/versions/9', { from sourceSets.java9.output }
    from sourceSets.main.output
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

assemble.dependsOn fatJar
